buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
        classpath 'org.ajoberstar:gradle-git-publish:3.0.0'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.15.0' apply false
    id 'org.jdrupes.vmoperator.java-doc-conventions'
    id 'eclipse'
}

project.group = 'org.jdrupes.vmoperator'

task stage {
    description = 'To be executed by CI, build and update JavaDoc.'
    group = 'build'

    // Build everything first
    gradle.projectsEvaluated {
        dependsOn subprojects.tasks.collect { 
            tc -> tc.findByName("build") }.flatten()
    }
    
    if (JavaVersion.current() == JavaVersion.VERSION_17) {
        // Publish JavaDoc
        dependsOn gitPublishPush
    }
}

eclipse {
    project {
        file {
            project.natures += 'org.eclipse.buildship.core.gradleprojectnature'
            project.buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
        }
    }
}
